/*
 * Copyright 2017-2018 Kai-Chung Yan (殷啟聰)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'pmd'
apply plugin: 'maven-publish'

sourceCompatibility = 8

dependencies {
  testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0")
  testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0")
}

checkstyle {
  def version = '8.7'
  toolVersion = version
  File styleFile = file("${rootProject.buildDir}/checkstyle.xml")
  if (!styleFile.exists()) {
    mkdir styleFile.parent
    styleFile.createNewFile()
    styleFile.write(new URL(
        "https://raw.githubusercontent.com/checkstyle/checkstyle/checkstyle-${version}/src/main/resources/google_checks.xml"
    ).text)
  }
  config = resources.text.fromFile(styleFile)
}

spotbugsMain {
  reports {
    html.enabled = true
    xml.enabled = false
  }
}

pmd {
  ruleSets = [
      'java-basic',
      'java-braces',
      'java-clone',
      'java-codesize',
      'java-controversial',
      'java-coupling',
      'java-design',
      'java-empty',
      'java-finalizers',
      'java-imports',
      'java-junit',
      'java-logging-java',
      'java-migrating',
      'java-naming',
      'java-optimizations',
      'java-strictexception',
      'java-strings',
      'java-sunsecure',
      'java-typeresolution',
      'java-unnecessary',
      'java-unusedcode'
  ]
}

publishing {
  publications {
    main(MavenPublication) {
      from components.java
    }
  }
}

clean.doLast {
  delete "${rootDir}/config/checkstyle/checkstyle.xml"
}